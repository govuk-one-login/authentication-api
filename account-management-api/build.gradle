plugins {
    id "java"
    id "jacoco"
}

group = "uk.gov.di.authentication.accountmanagement"
version = "unspecified"

configurations {
    compileOnly.extendsFrom(
            lambda,
            sqs,
            ssm,
            sns,
            s3,
            dynamodb
            )
    implementation.extendsFrom(
            nimbus,
            govuk_notify,
            gson,
            vavr
            )
    runtimeOnly.extendsFrom(logging_runtime)
    testImplementation.extendsFrom(
            tests,
            lambda_tests,
            lambda,
            sqs,
            dynamodb
            )
    testRuntimeOnly.extendsFrom(
            test_runtime
            )
}

dependencies {
    implementation project(":shared")
    testImplementation project(":shared-test")
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
