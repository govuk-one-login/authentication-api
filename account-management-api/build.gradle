plugins {
    id "java"
    id "jacoco"
}

group "uk.gov.di.authentication.accountmanagement"
version "unspecified"

configurations.compileOnly {
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.sqs)
    extendsFrom(configurations.ssm)
    extendsFrom(configurations.sns)
    extendsFrom(configurations.s3)
    extendsFrom(configurations.dynamodb)
}

configurations.implementation {
    extendsFrom(configurations.nimbus)
    extendsFrom(configurations.govuk_notify)
    extendsFrom(configurations.gson)
}

configurations.runtimeOnly.extendsFrom(configurations.logging_runtime)

configurations.testImplementation {
    extendsFrom(configurations.tests)
    extendsFrom(configurations.lambda_tests)
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.sqs)
    extendsFrom(configurations.dynamodb)
}

configurations.testRuntimeOnly.extendsFrom(configurations.test_runtime)

dependencies {
    implementation project(":shared")

    testImplementation project(":shared-test")
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.setRequired true
    }
    dependsOn "test"
}
