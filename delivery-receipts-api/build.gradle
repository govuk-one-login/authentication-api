plugins {
    id "java"
    id "jacoco"
}

group "uk.gov.di.authentication.deliveryreceiptsapi"
version "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/delivery-receipts")

dependencies {
    implementation libs.govUkNotify,
            libs.bundles.awsLambda,
            libs.gson,
            libs.libphonenumber,
            libs.awsCloudwatchEmf

    implementation project(":shared")

    runtimeOnly libs.bundles.loggingRuntime

    testImplementation libs.bundles.test,
            libs.awsLambdaTests,
            project(":shared-test"),
            libs.bundles.awsLambda,
            libs.bundles.awsDynamoDb

    testRuntimeOnly libs.bundles.testRuntime,
            platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
