plugins {
    id 'java'
    id "jacoco"
}

group 'uk.gov.di.authentication.utils'
version 'unspecified'

configurations.compileOnly {
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.s3)
    extendsFrom(configurations.dynamodb)
}

configurations.implementation {
    extendsFrom(configurations.govuk_notify)
    extendsFrom(configurations.nimbus)
    extendsFrom(configurations.xray)
}

configurations.runtimeOnly.extendsFrom(configurations.logging_runtime)


configurations.testImplementation {
    extendsFrom(configurations.tests)
    extendsFrom(configurations.s3)
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.dynamodb)
}

configurations.testRuntimeOnly.extendsFrom(configurations.test_runtime)


dependencies {
    implementation project(":shared")
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip

clean.dependsOn ":composeDown"

jacocoTestReport {
    reports {
        xml.setRequired true
    }
    dependsOn "test"
}
