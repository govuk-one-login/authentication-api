plugins {
    id 'java'
    id "jacoco"
}

group 'uk.gov.di.authentication.utils'
version 'unspecified'

File terraformDir = new File(project.rootDir, "ci/terraform/utils")

dependencies {
    compileOnly configurations.lambda,
            configurations.s3,
            configurations.dynamodb

    implementation project(":shared"),
            configurations.govuk_notify,
            configurations.nimbus,
            configurations.xray

    runtimeOnly configurations.logging_runtime

    testImplementation configurations.tests,
            configurations.s3,
            configurations.lambda,
            configurations.dynamodb

    testRuntimeOnly configurations.test_runtime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip

clean.dependsOn ":composeDown"

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn "test"
}
