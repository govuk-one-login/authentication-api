plugins {
    id "java"
    id "jacoco"
}

group = "uk.gov.di.authentication.utils"
version = "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/utils")

dependencies {
    compileOnly libs.bundles.awsLambda,
            libs.awsS3,
            libs.bundles.awsDynamoDb

    implementation project(":shared"),
            libs.govUkNotify,
            libs.bundles.nimbus,
            platform(libs.xrayBom),
            libs.bundles.xray,
            platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk,
            libs.awsCloudwatch,
            libs.gson

    runtimeOnly libs.bundles.loggingRuntime

    testImplementation libs.bundles.test,
            libs.awsS3,
            libs.bundles.awsLambda,
            libs.bundles.awsDynamoDb

    testRuntimeOnly libs.bundles.testRuntime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip

clean.dependsOn ":composeDown"

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
