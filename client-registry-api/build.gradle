plugins {
    id "java"
    id "jacoco"
}

group = "uk.gov.di.clientregistry"
version = "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/oidc")

dependencies {

    compileOnly libs.bundles.awsLambda,
            libs.awsSsm,
            libs.awsSns,
            libs.bundles.awsDynamoDb

    implementation libs.bundles.nimbus,
            libs.gson,
            libs.apacheCommonsValidator

    implementation project(":orchestration-shared")

    runtimeOnly libs.bundles.loggingRuntime,
            platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk

    testImplementation libs.bundles.test,
            libs.awsLambdaTests,
            project(":orchestration-shared-test"),
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.bundles.awsDynamoDb

    testRuntimeOnly libs.bundles.testRuntime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
