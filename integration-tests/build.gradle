plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.16"
}

group "uk.gov.di"
version "unspecified"

dependencies {
    testImplementation configurations.tests,
            configurations.apache,
            configurations.gson,
            configurations.nimbus,
            configurations.bouncycastle,
            configurations.sqs,
            configurations.kms,
            configurations.s3,
            configurations.cloudwatch,
            configurations.dynamodb,
            configurations.lettuce,
            configurations.lambda,
            configurations.pact_provider

    implementation project(":shared"), noXray
    implementation project(":orchestration-shared"), noXray
    implementation project(":shared-test"), noXray
    implementation project(":orchestration-shared-test"), noXray
    implementation project(":oidc-api"), noXray
    implementation project(":frontend-api"), noXray
    implementation project(":auth-external-api"), noXray
    implementation project(":client-registry-api"), noXray
    implementation project(":ipv-api"), noXray
    implementation project(":interventions-api-stub"), noXray
    implementation project(":doc-checking-app-api"), noXray
    implementation project(":utils"), noXray

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"
    testImplementation("uk.org.webcompere:system-stubs-jupiter:2.1.3")
    testImplementation("com.google.guava:guava:33.3.1-jre")
    testImplementation("org.awaitility:awaitility:4.2.0")
}

test {
    useJUnitPlatform()
    exclude 'uk/gov/di/authentication/contract/**'

    environment "AUDIT_SIGNING_KEY_ALIAS", "alias/local-audit-payload-signing-key-alias"
    environment "AWS_ACCESS_KEY_ID", "mock-access-key"
    environment "AWS_REGION", "eu-west-2"
    environment "AWS_SECRET_ACCESS_KEY", "mock-secret-key "
    environment "OIDC_API_BASE_URL", "http://localhost"
    environment "DEFAULT_LOGOUT_URI", "http://localhost:3000/signed-out"
    environment "DOMAIN_NAME", "localhost"
    environment "DOC_APP_DOMAIN", "https://doc-app"
    environment "DYNAMO_ENDPOINT", "http://localhost:8000"
    environment "ENVIRONMENT", "local"
    environment "LOCALSTACK_ENDPOINT", "http://localhost:45678"
    environment "LOGIN_URI", "http://localhost:3000"
    environment "ROOT_RESOURCE_URL", "http://localhost"
    environment "REDIS_KEY", "session"
    environment "RESET_PASSWORD_URL", "http://localhost:3000/reset-password?code="
    environment "SQS_ENDPOINT", "http://localhost:45678"
    environment "STUB_RELYING_PARTY_REDIRECT_URI", "https://rp-build.build.stubs.account.gov.uk/"
    environment "TERMS_CONDITIONS_VERSION", "1.0"
    environment "HEADERS_CASE_INSENSITIVE", "true"
    environment "IDENTITY_ENABLED", "false"
    environment "TRACING_ENABLED", "false"
    environment "INTERNAl_SECTOR_URI", "https://test.account.gov.uk"
    environment "BULK_USER_EMAIL_INCLUDED_TERMS_AND_CONDITIONS", "1.0,1.1,1.2,1.3,1.4"
    environment "SEND_STORAGE_TOKEN_TO_IPV_ENABLED", "true"

    testLogging {
        showStandardStreams = false
    }

    doLast {
        tasks.getByName("jacocoTestReport").sourceDirectories.from(
                project(":frontend-api").sourceSets.main.java,
                project(":oidc-api").sourceSets.main.java,
                project(":client-registry-api").sourceSets.main.java,
                project(":shared").sourceSets.main.java,
                project(":utils").sourceSets.main.java)

        tasks.getByName("jacocoTestReport").classDirectories.from(
                project(":client-registry-api").sourceSets.main.output,
                project(":frontend-api").sourceSets.main.output,
                project(":oidc-api").sourceSets.main.output,
                project(":shared").sourceSets.main.output,
                project(":utils").sourceSets.main.output)
    }
    dependsOn ":composeUp"
    finalizedBy ":composeDown"
}

pactPublish.enabled = false

task pactProviderTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include 'uk/gov/di/authentication/contract/**'
    environment "PROVIDER_UNDER_TEST", "ClientRegistryProvider"

    environment "AWS_ACCESS_KEY_ID", "mock-access-key"
    environment "AWS_REGION", "eu-west-2"
    environment "DYNAMO_ENDPOINT", "http://localhost:8000"
    environment "REDIS_KEY", "session"
    environment "TRACING_ENABLED", "false"

    dependsOn ":composeUp"
    finalizedBy ":composeDown"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
