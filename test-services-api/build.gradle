plugins {
    id "java"
    id "jacoco"
}

group = "uk.gov.di.authentication.testservices"
version = "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/test-services")

dependencies {
    compileOnly libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsSsm,
            libs.awsSns,
            libs.awsKms,
            libs.bundles.awsDynamoDb

    implementation libs.gson,
            libs.bundles.nimbus

    implementation project(":shared")

    runtimeOnly libs.bundles.loggingRuntime,
            platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk

    testImplementation libs.bundles.test,
            libs.bundles.apache,
            libs.awsLambdaTests,
            project(":shared-test"),
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsKms,
            libs.bundles.awsDynamoDb

    testRuntimeOnly libs.bundles.testRuntime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
