plugins {
    id "application"
    id "java"
}

group = "uk.gov.di.authentication.local"
version = "unspecified"

dependencies {
    implementation libs.javalin,
            libs.bundles.awsDynamoDb,
            libs.awsKms,
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsSsm,
            libs.bundles.nimbus,
            project(":shared"),
            project(":auth-external-api"),
            project(":frontend-api")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = "uk.gov.di.authentication.local.App"
    applicationDefaultJvmArgs = [
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5402"
    ]
}

run {
    // Load env file when running from gradle
    def envFile = file(".env.local")
    def envProperties = new Properties()
    envFile.withReader("UTF-8") {reader ->
        envProperties.load(reader)
    }
    envProperties.entrySet().forEach {entry ->
        environment entry.key.toString(), entry.value.toString()
    }
}

sonar {
    properties {
        property 'sonar.coverage.exclusions', '**/uk/gov/di/authentication/local/**'
    }
}
