plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.17"
}

group "uk.gov.di.authentication.frontendapi"
version "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/oidc")

dependencies {

    implementation project(path: ':shared')
    implementation project(':user-permissions')
    implementation project(':audit-events')

    compileOnly configurations.lambda,
            configurations.sqs,
            configurations.ssm,
            configurations.sns,
            configurations.s3,
            configurations.dynamodb,
            configurations.kms

    implementation configurations.govuk_notify,
            configurations.apache,
            configurations.gson,
            configurations.nimbus,
            configurations.cloudwatch,
            configurations.bouncycastle,
            configurations.kms,
            configurations.jetbrains_annotations

    runtimeOnly configurations.logging_runtime,
            configurations.open_telemetry


    testImplementation configurations.tests,
            configurations.lambda_tests,
            project(":shared-test"),
            configurations.lambda,
            configurations.sqs,
            configurations.s3,
            configurations.kms,
            configurations.dynamodb,
            configurations.pact_consumer,
            'org.wiremock:wiremock-jetty12:3.13.1'

    testRuntimeOnly configurations.test_runtime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/frontendapi/contract/**'
}

var pactDir = "$rootDir/frontend-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include 'uk/gov/di/authentication/frontendapi/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn "test"
}
