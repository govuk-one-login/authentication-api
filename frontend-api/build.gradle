plugins {
    id "java"
    id "jacoco"
    alias libs.plugins.pact
    id "org.openapi.generator" version "7.2.0"
}

group = "uk.gov.di.authentication.frontendapi"
version = "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/oidc")

dependencies {
    implementation project(path: ':shared')
    implementation project(':user-permissions')
    implementation project(':audit-events')

    compileOnly libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsSsm,
            libs.awsSns,
            libs.awsS3,
            libs.bundles.awsDynamoDb,
            libs.awsKms,
            libs.awsSecretsManager

    implementation libs.govUkNotify,
            libs.bundles.apache,
            libs.gson,
            libs.bundles.nimbus,
            libs.awsCloudwatchEmf,
            libs.awsCloudwatch,
            libs.bouncyCastle,
            libs.awsKms,
            libs.jetbrainsAnnotations,
            libs.webauthnServer,
            libs.jacksonDatabind

    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    runtimeOnly libs.bundles.loggingRuntime,
            platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk

    testImplementation libs.bundles.test,
            libs.awsLambdaTests,
            project(":shared-test"),
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsS3,
            libs.awsKms,
            libs.bundles.awsDynamoDb,
            libs.pactConsumer,
            libs.wiremock

    testRuntimeOnly libs.bundles.testRuntime
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/resources/jwt-contracts/amc-access-token.yaml"
    outputDir = layout.buildDirectory.dir("generated/openapi").get().asFile.path
    modelPackage = "uk.gov.di.authentication.frontendapi.model"

    invokerPackage = "uk.gov.di.authentication.frontendapi.helpers"

    // Use okhttp-gson to ensure we get @SerializedName annotations
    library = "okhttp-gson"

    globalProperties = [
        models: "",
        apis: "false",
        modelTests: "false",
        apiTests: "false",
        modelDocs: "false",

        // CRITICAL FIX: Turn OFF supporting files.
        // This prevents the generator from creating the 'JSON.java' file that
        // imports forbidden libraries (gson-fire, okio).
        // Provided our own JSON.java file at /frontendapi/helpers/JSON.java
        supportingFiles: "false"
    ]

    configOptions = [
        serializationLibrary: "gson",
        useSwaggerAnnotations: "false",
        useBeanValidation: "false",
        openApiNullable: "false",
        useJakartaEe: "true",      // Correct for Java 17
        dateLibrary: "java8",      // Correct for Java 17
        hideGenerationTimestamp: "true",

        // Ensure forward compatibility (ignore unknown fields instead of crashing)
        disallowAdditionalPropertiesIfNotPresent: "false"
    ]
}

sourceSets.main.java.srcDir layout.buildDirectory.dir("generated/openapi/src/main/java")

compileJava.dependsOn tasks.openApiGenerate

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/frontendapi/contract/**'
}

var pactDir = "$rootDir/frontend-api/src/test/pact"

tasks.register('pactConsumerTests', Test) {
    group = "verification"
    useJUnitPlatform()
    include 'uk/gov/di/authentication/frontendapi/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

tasks.register('buildZip', Zip) {
    if (project.hasProperty('isDevDeployBuild') && project.isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
