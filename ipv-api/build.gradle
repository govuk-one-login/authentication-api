plugins {
    id "java"
    id "jacoco"
    alias(libs.plugins.pact)
}

group = "uk.gov.di.authentication.ipvapi"
version = "unspecified"

dependencies {
    testImplementation(libs.lambda.tests)
    testImplementation(libs.bundles.aws.lambda)
    testImplementation(libs.aws.sqs)
    testImplementation(libs.aws.kms)
    testImplementation(libs.bundles.dynamodb)
    testImplementation(libs.pact.consumer)

    runtimeOnly(libs.bundles.runtime.logging)
    testRuntimeOnly(libs.junit.engine)

    implementation(libs.gson)
    compileOnly(libs.bundles.aws.lambda)
    compileOnly(libs.aws.sqs)
    compileOnly(libs.aws.ssm)
    compileOnly(libs.aws.sns)
    compileOnly(libs.aws.kms)
    compileOnly(libs.bundles.dynamodb)

    implementation project(":orchestration-shared")
    implementation(libs.bundles.nimbusds)
    testImplementation project(":orchestration-shared-test")
    testImplementation(libs.bundles.testing)
    testImplementation(libs.bundles.apache)
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/ipv/contract/**'
}

var pactDir = "$rootDir/ipv-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include 'uk/gov/di/authentication/ipv/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
