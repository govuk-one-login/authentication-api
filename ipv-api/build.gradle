plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.5"
}

group "uk.gov.di.authentication.ipvapi"
version "unspecified"

dependencies {
    compileOnly configurations.lambda,
            configurations.sqs,
            configurations.ssm,
            configurations.sns,
            configurations.kms,
            configurations.dynamodb

    implementation configurations.gson,
            configurations.nimbus

    implementation project(":orchestration-shared")

    runtimeOnly configurations.logging_runtime

    testImplementation configurations.tests,
            configurations.apache,
            configurations.lambda_tests,
            project(":orchestration-shared-test"),
            configurations.lambda,
            configurations.sqs,
            configurations.kms,
            configurations.dynamodb,
            configurations.pact_consumer

    testRuntimeOnly configurations.test_runtime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/ipv/contract/**'
}

var pactDir = "$rootDir/ipv-api/src/test/pact"

task contractTest (type: Test) {
    useJUnitPlatform()
    include '/uk/gov/di/authentication/ipv/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" +"$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER"
        pactBrokerPassword = "$System.env.PACT_PASSWORD"
        consumerVersion = "$System.env.CONSUMER_APP_VERSION"
        //        pactBrokerAuthenticationHeader = "$System.env.PACT_BROKER_SOURCE_HEADER"
        tags = [
            "$System.env.GIT_BRANCH"
        ]
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn "test"
}
