import java.util.stream.Collectors

plugins {
    id "com.diffplug.spotless" version "5.17.1"
    id "com.avast.gradle.docker-compose" version "0.14.9"
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

ext {
    dependencyVersions = [
        jackson_version: "2.13.0",
        aws_sdk_version: "1.12.120",
        aws_lambda_core_version: "1.2.1",
        aws_lambda_events_version: "3.11.0",
        nimbusds_oauth_version: "9.20",
        nimbusds_jwt_version: "9.15.1",
        junit: "5.8.2",
        jackson_version: "2.13.0",
        glassfish_version: "3.0.3",
    ]

    terraformEnvironment = project.properties["terraformEnvironment"] ?: "localstack"

    awsCredentialsEnvironment = terraformEnvironment == "localstack" ? [
        AWS_ACCESS_KEY_ID: "mock-access-key",
        AWS_SECRET_ACCESS_KEY: "mock-secret-key",
    ] : [:]
}

subprojects {
    configurations {
        bouncycastle
        dynamodb
        glassfish
        govuk_notify
        hamcrest
        jackson
        lambda
        lambda_tests
        lettuce
        logging_runtime
        nimbus
        s3
        sns
        sqs
        ssm
        tests
        test_runtime
    }

    dependencies {
        bouncycastle "org.bouncycastle:bcpkix-jdk15on:1.69"

        dynamodb "com.amazonaws:aws-java-sdk-dynamodb:${dependencyVersions.aws_sdk_version}"

        glassfish "org.glassfish.jersey.core:jersey-client:${dependencyVersions.glassfish_version}",
                "org.glassfish.jersey.inject:jersey-hk2:${dependencyVersions.glassfish_version}",
                "org.glassfish.jersey.media:jersey-media-json-jackson:${dependencyVersions.glassfish_version}",
                "jakarta.activation:jakarta.activation-api:2.0.1"

        govuk_notify "uk.gov.service.notify:notifications-java-client:3.17.2-RELEASE"

        hamcrest "org.hamcrest:hamcrest:2.2"

        lambda "com.amazonaws:aws-lambda-java-core:${dependencyVersions.aws_lambda_core_version}",
                "com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}",
                "com.amazonaws:aws-lambda-java-log4j2:1.2.0"

        lambda_tests "com.amazonaws:aws-lambda-java-tests:1.1.1"

        lettuce "org.apache.commons:commons-pool2:2.11.1",
                "io.lettuce:lettuce-core:6.1.5.RELEASE"

        logging_runtime "com.amazonaws:aws-lambda-java-log4j2:1.2.0",
                "org.slf4j:slf4j-nop:1.7.32"

        jackson "com.fasterxml.jackson.core:jackson-core:${dependencyVersions.jackson_version}",
                "com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
                "com.fasterxml.jackson.core:jackson-annotations:${dependencyVersions.jackson_version}",
                "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
                "org.hibernate.validator:hibernate-validator:7.0.1.Final",
                "org.glassfish:jakarta.el:4.0.2"

        nimbus "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
                "com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}"

        sns "com.amazonaws:aws-java-sdk-sns:${dependencyVersions.aws_sdk_version}"

        sqs "software.amazon.awssdk:sqs:2.17.99"

        ssm "com.amazonaws:aws-java-sdk-ssm:${dependencyVersions.aws_sdk_version}"

        s3 "com.amazonaws:aws-java-sdk-s3:${dependencyVersions.aws_sdk_version}"

        tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
                "org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
                "org.mockito:mockito-core:4.1.0",
                "org.awaitility:awaitility:4.1.1",
                configurations.hamcrest

        test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"
    }
}

spotless {
    java {
        target "**/*.java"
        googleJavaFormat("1.12.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }

    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

dockerCompose {
    buildBeforeUp = false
    forceRecreate = false

    startedServices = [
        'aws',
        'dynamodb',
        'redis'
    ]
    def logDir = new File(project.buildDir, "logs")
    if (!logDir.exists()) {
        println("creating logs folder...")
        logDir.mkdir()
    }
    captureContainersOutput = false
    captureContainersOutputToFile = new File('logs', 'docker-compose-gradle.log')
    projectName = rootProject.rootDir.name
}

compileJava.dependsOn "spotlessApply"

clean.dependsOn "composeDown"

abstract class Terraform extends DefaultTask {

    Terraform() {
        group = "terraform"
    }

    @Input
    abstract MapProperty<String, String> getEnvironmentVars()

    @InputDirectory
    abstract DirectoryProperty getTerraformDirectory()

    @Input
    abstract Property<String> getEnvironmentName()

    @Internal
    Object terraformOutputs

    @TaskAction
    void apply() {
        def tfEnvironment = [
            "TF_DATA_DIR": "${project.buildDir}/terraform/${environmentName.get()}-${terraformDirectory.get().asFile.name}",
            "TF_IN_AUTOMATION": "1"
        ] + environmentVars.get()

        logger.lifecycle("Running Terraform Initialise...")
        project.exec {
            executable "terraform"
            workingDir terraformDirectory.asFile
            environment tfEnvironment
            args "init", "-backend-config=${environmentName.get()}.hcl"
        }

        logger.lifecycle("Applying ${terraformDirectory.getAsFile().get().name} Terraform (run gradle with --info to see output)...")
        project.exec {
            logging.captureStandardOutput(LogLevel.INFO)
            executable "terraform"
            workingDir terraformDirectory.asFile
            environment tfEnvironment
            args "apply", "-auto-approve", "-var-file=${environmentName.get()}.tfvars"
        }

        logger.lifecycle("Getting Terraform Outputs!")
        new ByteArrayOutputStream().withStream { os ->
            project.exec {
                standardOutput os
                executable "terraform"
                workingDir terraformDirectory.asFile
                environment tfEnvironment
                args "output", "-json"
            }
            this.terraformOutputs = new groovy.json.JsonSlurper().parse(os.toByteArray())
        }
        logger.lifecycle("Terraform Complete!")
    }

    protected Object outputs() {
        return this.terraformOutputs
    }
}

task sharedTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/shared")
    environmentName = terraformEnvironment
    environmentVars = awsCredentialsEnvironment

    if (terraformEnvironment == "localstack") {
        dependsOn "composeUp"
    }
    dependsOn ":lambda-warmer:buildZip"
    dependsOn ":account-migrations:buildZip"
}

task oidcTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/oidc")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    doLast {
        def json = outputs()
        allprojects.findAll {it.name == "integration-tests"}.first().tasks.getByName("test") {
            environment "API_GATEWAY_ID", json.api_gateway_root_id.value
            environment "TOKEN_SIGNING_KEY_ALIAS", json.token_signing_key_alias.value
            environment "BASE_URL", json.base_url.value
            environment "API_KEY", json.frontend_api_key.value
            environment "FRONTEND_API_GATEWAY_ID", json.frontend_api_gateway_root_id.value
            environment "FRONTEND_API_KEY", json.frontend_api_key.value
            environment "EVENTS_SNS_TOPIC_ARN", json.events_sns_topic_arn.value
            environment "EMAIL_QUEUE_URL", json.email_queue.value
        }
        allprojects.findAll {it.name == "account-management-integration-tests"}.first().tasks.getByName("test") {
            environment "EVENTS_SNS_TOPIC_ARN", json.events_sns_topic_arn.value
        }
    }
    dependsOn ":client-registry-api:buildZip"
    dependsOn ":frontend-api:buildZip"
    dependsOn ":oidc-api:buildZip"
    dependsOn "sharedTerraform"
}

task auditTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/audit-processors")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    dependsOn ":audit-processors:buildZip"
    dependsOn "oidcTerraform"
}

task acctMgmtTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/account-management")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    doLast {
        def json = outputs()
        allprojects.findAll {it.name == "account-management-integration-tests"}.first().tasks.getByName("test") {
            environment "API_GATEWAY_ID", json.api_gateway_root_id.value
            environment "BASE_URL", json.base_url.value
            environment "EMAIL_QUEUE_URL", json.email_queue.value
        }
    }
    dependsOn ":account-management-api:buildZip"
    dependsOn "auditTerraform"
}

sonarqube {
    properties {
        property "sonar.projectKey", "alphagov_di-authentication-api"
        property "sonar.organization", "alphagov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn "test"
}
