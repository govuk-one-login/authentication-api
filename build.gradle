import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
buildscript {
    repositories {
        maven {
            url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
        }
    }
    dependencies {
        constraints {
            classpath('org.apache.commons:commons-lang3:[3.18.0,)') {
                because 'CVE-2025-48924 is fixed in org.apache.commons:commons-lang3:3.18.0 and higher'
            }
        }
    }
}

plugins {
    id "com.diffplug.spotless" version "7.2.1"
    id "com.avast.gradle.docker-compose" version "0.17.12"
    id "org.sonarqube" version "6.2.0.5505"
    id "jacoco-report-aggregation"
    id 'com.github.spotbugs' version '6.2.4'
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

ext {
    dependencyVersions = [
        aws_sdk_v2_version: "2.32.19",
        aws_lambda_core_version: "1.3.0",
        aws_lambda_events_version: "3.16.1",
        gson: "2.13.1",
        java_parser: "3.27.0",
        jetbrains_annotations: "26.0.2",
        jgit: "6.10.1.202505221210-r",
        nimbusds_oauth_version: "10.13.2",
        nimbusds_jwt_version: "10.4.1",
        junit: "5.13.4",
        xray: "2.19.0",
        pact: "4.6.17",
    ]

    terraformEnvironment = project.properties["terraformEnvironment"] ?: "localstack"

    awsCredentialsEnvironment = terraformEnvironment == "localstack" ? [
        AWS_ACCESS_KEY_ID: "mockaccesskey",
        AWS_SECRET_ACCESS_KEY: "mocksecretkey",
    ] : [:]

    noXray = {
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-bom"
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-core"
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-aws-sdk-core"
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-aws-sdk-instrumentor"
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-aws-sdk-v2"
        exclude group: "com.amazonaws", module: "aws-xray-recorder-sdk-aws-sdk-v2-instrumentor"
    }
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
    buildscript {
        dependencies {
            constraints {
                // Netty constraints
                classpath('io.netty:netty-codec-http:[4.1.108.Final,)') {
                    because 'CVE-2024-29025 is fixed in io.netty:netty-codec-http:4.1.108.Final and higher'
                }

                classpath('io.netty:netty-handler:[4.1.118.Final,)') {
                    because 'CVE-2025-24970 is fixed in io.netty:netty-handler:4.1.118.Final and higher'
                }

                classpath('io.netty:netty-codec-http2:[4.1.124.Final,4.2.0)') {
                    because 'CVE-2025-55163 is fixed in io.netty:netty-codec-http2:4.1.124.Final and higher'
                }

                // Apache Commons constraints
                classpath('commons-beanutils:commons-beanutils:[1.11.0,)') {
                    because 'CVE-2025-48734 is fixed in commons-beanutils:commons-beanutils:1.11.0 and higher'
                }

                classpath('org.apache.commons:commons-lang3:[3.18.0,)') {
                    because 'CVE-2025-48924 is fixed in org.apache.commons:commons-lang3:3.18.0 and higher'
                }

                // Other constraints
                classpath('com.google.protobuf:protobuf-java:[3.25.5,)') {
                    because 'CVE-2024-7254 is fixed in 3.25.5 and above'
                }
            }
        }
    }

    apply plugin: "com.github.spotbugs"
    apply plugin: "java"

    ext {
        isDevDeployBuild = project.hasProperty("devDeployBuild") ? project.getProperty("devDeployBuild").toBoolean() : false
    }

    spotbugs {
        ignoreFailures = false
        showStackTraces = true
        effort = Effort.valueOf('MAX')
        reportLevel = Confidence.valueOf('HIGH')
        excludeFilter = file("$rootProject.projectDir/spotbugsExclude.xml")
    }

    configurations {
        apache
        bouncycastle
        cloudwatch
        dynamodb
        govuk_notify
        gson
        hamcrest
        java_parser
        javalin
        jetbrains_annotations
        jgit
        kms
        lambda
        lambda_tests
        lettuce
        libphonenumber
        logging_runtime
        nimbus
        secretsmanager
        s3
        sns
        sqs
        ssm
        tests
        test_runtime
        xray
        pact_consumer
        pact_provider
        open_telemetry
    }

    // Check dependencies using:
    //
    // ./gradlew :frontend-api:dependencyInsight --dependency guava  --configuration testRuntimeClasspath
    // ./gradlew :frontend-api:dependencyInsight --dependency guava  --configuration runtimeClasspath
    //
    dependencies {
        constraints {
            configurations.configureEach { conf ->
                // Netty constraints
                add(conf.name, 'io.netty:netty-codec-http:[4.1.108.Final,)') {
                    because 'CVE-2024-29025 is fixed in io.netty:netty-codec-http:4.1.108.Final and higher'
                }

                add(conf.name, 'io.netty:netty-common:[4.1.115.Final,4.2)') {
                    because 'CVE-2024-47535 is fixed in io.netty:netty-common:4.1.115.Final and higher'
                }

                add(conf.name, 'io.netty:netty-handler:[4.1.118.Final,)') {
                    because 'CVE-2025-24970 is fixed in io.netty:netty-handler:4.1.118.Final and higher'
                }

                add(conf.name, 'io.netty:netty-codec-http2:[4.1.124.Final,4.2.0)') {
                    because 'CVE-2025-55163 is fixed in io.netty:netty-codec-http2:4.1.124.Final and higher'
                }

                // Jetty constraints
                add(conf.name, 'org.eclipse.jetty.http2:jetty-http2-common:[12.0.17,12.1.0)') {
                    because 'CVE-2025-1948 is fixed in org.eclipse.jetty.http2:jetty-http2-common:12.0.17 and higher'
                }

                add(conf.name, 'org.eclipse.jetty:jetty-http:[12.0.12,)') {
                    because 'CVE-2024-6763 is fixed in org.eclipse.jetty:jetty-http:12.0.12 and higher'
                }

                // Jackson constraints
                add(conf.name, 'com.fasterxml.jackson.core:jackson-databind:[2.12.7.1,)') {
                    because 'CVE-2022-42003 is fixed in 2.12.7.1 and above'
                }

                add(conf.name, 'com.fasterxml.jackson.core:jackson-core:[2.15.0,)') {
                    because 'CVE-2025-52999 is fixed in com.fasterxml.jackson.core:jackson-core:2.15.0 and higher'
                }

                // Apache Commons constraints
                add(conf.name, 'commons-beanutils:commons-beanutils:[1.11.0,)') {
                    because 'CVE-2025-48734 is fixed in commons-beanutils:commons-beanutils:1.11.0 and higher'
                }

                add(conf.name, 'org.apache.commons:commons-lang3:[3.18.0,)') {
                    because 'CVE-2025-48924 is fixed in org.apache.commons:commons-lang3:3.18.0 and higher'
                }

                // Other constraints
                add(conf.name, 'com.google.protobuf:protobuf-java:[3.25.5,)') {
                    because 'CVE-2024-7254 is fixed in 3.25.5 and above'
                }
            }
        }

        apache "commons-codec:commons-codec:1.19.0",
                "org.apache.httpcomponents:httpclient:4.5.14",
                "org.apache.commons:commons-collections4:4.5.0",
                "commons-net:commons-net:3.12.0",
                "commons-io:commons-io:2.20.0",
                "org.apache.commons:commons-lang3:3.18.0"

        bouncycastle "org.bouncycastle:bcpkix-jdk18on:1.81"

        cloudwatch "software.amazon.cloudwatchlogs:aws-embedded-metrics:4.2.0"

        dynamodb "software.amazon.awssdk:dynamodb:${dependencyVersions.aws_sdk_v2_version}",
                "software.amazon.awssdk:dynamodb-enhanced:${dependencyVersions.aws_sdk_v2_version}"

        lambda "software.amazon.awssdk:lambda:${dependencyVersions.aws_sdk_v2_version}"

        govuk_notify "uk.gov.service.notify:notifications-java-client:5.2.1-RELEASE"

        gson "com.google.code.gson:gson:${dependencyVersions.gson}"

        hamcrest "org.hamcrest:hamcrest:3.0"

        java_parser "com.github.javaparser:javaparser-core:${dependencyVersions.java_parser}"

        javalin "io.javalin:javalin:6.7.0"

        jetbrains_annotations "org.jetbrains:annotations:${dependencyVersions.jetbrains_annotations}"

        jgit "org.eclipse.jgit:org.eclipse.jgit:${dependencyVersions.jgit}"

        kms "software.amazon.awssdk:kms:${dependencyVersions.aws_sdk_v2_version}"

        lambda "com.amazonaws:aws-lambda-java-core:${dependencyVersions.aws_lambda_core_version}",
                "com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}",
                "com.amazonaws:aws-lambda-java-log4j2:1.6.0"

        lambda_tests "com.amazonaws:aws-lambda-java-tests:1.1.2"

        lettuce "org.apache.commons:commons-pool2:2.12.1",
                "io.lettuce:lettuce-core:6.7.1.RELEASE"

        libphonenumber "com.googlecode.libphonenumber:libphonenumber:9.0.12"

        logging_runtime "com.amazonaws:aws-lambda-java-log4j2:1.6.0",
                "org.slf4j:slf4j-nop:2.0.17"

        nimbus "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
                "com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}"

        secretsmanager "software.amazon.awssdk:secretsmanager:${dependencyVersions.aws_sdk_v2_version}"

        sns "software.amazon.awssdk:sns:${dependencyVersions.aws_sdk_v2_version}"

        sqs "software.amazon.awssdk:sqs:${dependencyVersions.aws_sdk_v2_version}"

        ssm "software.amazon.awssdk:ssm:${dependencyVersions.aws_sdk_v2_version}"

        s3 "software.amazon.awssdk:s3:${dependencyVersions.aws_sdk_v2_version}"

        tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
                "org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
                "org.junit.platform:junit-platform-launcher",
                "org.mockito:mockito-core:5.18.0",
                "org.awaitility:awaitility:4.3.0",
                "com.approvaltests:approvaltests:25.0.23",
                configurations.hamcrest,
                "org.reflections:reflections:0.10.2",
                "uk.org.webcompere:system-stubs-jupiter:2.1.8"

        test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"

        xray platform("com.amazonaws:aws-xray-recorder-sdk-bom:${dependencyVersions.xray}"),
                "com.amazonaws:aws-xray-recorder-sdk-core:${dependencyVersions.xray}",
                "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-instrumentor:${dependencyVersions.xray}",
                "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-core:${dependencyVersions.xray}",
                "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:${dependencyVersions.xray}",
                "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2-instrumentor:${dependencyVersions.xray}"

        spotbugs "com.github.spotbugs:spotbugs:4.9.4"

        spotbugsPlugins "software.amazon.lambda.snapstart:aws-lambda-snapstart-java-rules:0.2.1"

        pact_consumer "au.com.dius.pact.consumer:junit5:${dependencyVersions.pact}"

        pact_provider "au.com.dius.pact.provider:junit5:${dependencyVersions.pact}"

        open_telemetry platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:2.18.1-alpha"),
                "io.opentelemetry.instrumentation:opentelemetry-aws-sdk-2.2-autoconfigure",
                "io.opentelemetry.instrumentation:opentelemetry-java-http-client"
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Werror"
        }
        tasks.withType(AbstractArchiveTask) {
            // Ensure that the jar / zip files are reproducible
            includeEmptyDirs = false
            preserveFileTimestamps = false
            reproducibleFileOrder = true
        }
    }
}

spotless {
    java {
        target "**/*.java"
        googleJavaFormat("1.18.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }

    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

dockerCompose {
    buildBeforeUp = true
    forceRecreate = false

    if (System.getProperty("os.arch") == "aarch64") {
        useComposeFiles = [
            "docker-compose.yml",
            "docker-compose.arm64.yml"
        ]
    }

    startedServices = [
        'aws',
        'dynamodb',
        'redis'
    ]
    def logDir = new File(project.buildDir, "logs")
    if (!logDir.exists()) {
        println("creating logs folder...")
        logDir.mkdir()
    }
    captureContainersOutput = false
    captureContainersOutputToFile = new File('logs', 'docker-compose-gradle.log')
    projectName = rootProject.rootDir.name
}

compileJava.dependsOn "spotlessApply"

clean.dependsOn "composeDown"

abstract class Terraform extends DefaultTask {

    Terraform() {
        group = "terraform"
    }

    @Input
    abstract MapProperty<String, String> getEnvironmentVars()

    @InputDirectory
    abstract DirectoryProperty getTerraformDirectory()

    @Input
    abstract Property<String> getEnvironmentName()

    @Internal
    Object terraformOutputs

    @TaskAction
    void apply() {
        def tfEnvironment = [
            "TF_DATA_DIR": "${project.buildDir}/terraform/${environmentName.get()}-${terraformDirectory.get().asFile.name}",
            "TF_IN_AUTOMATION": "1"
        ] + environmentVars.get()

        logger.lifecycle("Running Terraform Initialise...")
        project.exec {
            executable "terraform"
            workingDir terraformDirectory.asFile
            environment tfEnvironment
            args "init", "-backend-config=${environmentName.get()}.hcl"
        }

        logger.lifecycle("Applying ${terraformDirectory.getAsFile().get().name} Terraform (run gradle with --info to see output)...")
        project.exec {
            logging.captureStandardOutput(LogLevel.INFO)
            executable "terraform"
            workingDir terraformDirectory.asFile
            environment tfEnvironment
            args "apply", "-auto-approve", "-var-file=${environmentName.get()}.tfvars"
        }

        logger.lifecycle("Getting Terraform Outputs!")
        new ByteArrayOutputStream().withStream { os ->
            project.exec {
                standardOutput os
                executable "terraform"
                workingDir terraformDirectory.asFile
                environment tfEnvironment
                args "output", "-json"
            }
            this.terraformOutputs = new groovy.json.JsonSlurper().parse(os.toByteArray())
        }
        logger.lifecycle("Terraform Complete!")
    }

    protected Object outputs() {
        return this.terraformOutputs
    }
}

task sharedTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/shared")
    environmentName = terraformEnvironment
    environmentVars = awsCredentialsEnvironment

    if (terraformEnvironment == "localstack") {
        dependsOn "composeUp"
    }
}

task oidcTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/oidc")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    doLast {
        def json = outputs()
        allprojects.findAll {it.name == "integration-tests"}.first().tasks.getByName("test") {
            environment "API_GATEWAY_ID", json.api_gateway_root_id.value
            environment "EXTERNAL_TOKEN_SIGNING_KEY_ALIAS", json.id_token_signing_key_alias.value
            environment "OIDC_API_BASE_URL", json.base_url.value
            environment "API_KEY", json.frontend_api_key.value
            environment "FRONTEND_API_GATEWAY_ID", json.frontend_api_gateway_root_id.value
            environment "FRONTEND_API_KEY", json.frontend_api_key.value
            environment "EVENTS_SNS_TOPIC_ARN", json.events_sns_topic_arn.value
            environment "EMAIL_QUEUE_URL", json.email_queue.value
            environment "EXPERIAN_PHONE_CHECK_QUEUE_URL", json.experian_phone_check_queue.value
        }
        allprojects.findAll {it.name == "account-management-integration-tests"}.first().tasks.getByName("test") {
            environment "EVENTS_SNS_TOPIC_ARN", json.events_sns_topic_arn.value
        }
    }
    dependsOn ":client-registry-api:buildZip"
    dependsOn ":frontend-api:buildZip"
    dependsOn ":oidc-api:buildZip"
    dependsOn ":ipv-api:buildZip"
    dependsOn ":doc-checking-app-api:buildZip"
    dependsOn "sharedTerraform"
}

task acctMgmtTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/account-management")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    doLast {
        def json = outputs()
        allprojects.findAll {it.name == "account-management-integration-tests"}.first().tasks.getByName("test") {
            environment "API_GATEWAY_ID", json.api_gateway_root_id.value
            environment "OIDC_API_BASE_URL", json.base_url.value
            environment "EMAIL_QUEUE_URL", json.email_queue.value
        }
    }
    dependsOn ":account-management-api:buildZip"
    dependsOn "auditTerraform"
}

task utilsTerraform (type: Terraform) {
    terraformDirectory = layout.projectDirectory.dir("ci/terraform/utils")
    environmentName = project.properties["terraformEnvironment"] ?: "localstack"
    environmentVars = awsCredentialsEnvironment

    dependsOn ":utils:buildZip"
    dependsOn "oidcTerraform"
}

String jacocoBuildReportPath = "reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"

sonar {
    properties {
        property "sonar.projectKey", "govuk-one-login_authentication-api"
        property "sonar.organization", "govuk-one-login"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", "**/shared-test/**,**/orchestration-shared-test/**,**/orchestration-shared-test/**"
        property "sonar.coverage.jacoco.xmlReportPaths", "$project.buildDir/$jacocoBuildReportPath"
    }
}

subprojects {
    sonar {
        properties {
            property 'sonar.coverage.jacoco.xmlReportPaths', "$projectDir.parentFile.path/build/$jacocoBuildReportPath"
        }
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

dependencies {
    jacocoAggregation subprojects
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn "test"
}

task searchForDependency {
    group = 'Custom'
    description = 'Find all projects that have a dependency'

    doLast {
        def targetGroup = project.hasProperty('targetGroup') ? project.property('targetGroup') : null
        def targetName = project.hasProperty('targetName') ? project.property('targetName') : null

        println "searching for ${targetGroup}:${targetName}...\n"

        def foundProjects = new LinkedHashSet<String>()

        allprojects {project ->
            project.configurations.each { config ->
                if (config.canBeResolved) {
                    def resolvedDependencies = config.resolvedConfiguration.lenientConfiguration.allModuleDependencies

                    resolvedDependencies.each { dep ->
                        if (dep.moduleGroup == targetGroup && dep.moduleName == targetName) {
                            foundProjects.add(project.name + ":" + config.name)
                        }
                    }
                }
            }
        }

        foundProjects.each { projectName ->
            println "${projectName}"
        }
    }
}
