plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.17"
}

group "uk.gov.di.authentication.oidc"
version "unspecified"

File terraformDir = new File(project.rootDir, "ci/terraform/oidc")

dependencies {
    compileOnly libs.awsKms,
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsSns,
            libs.bundles.awsDynamoDb

    implementation libs.govUkNotify,
            libs.gson,
            libs.bundles.nimbus,
            libs.bouncyCastle,
            libs.awsCloudwatchEmf,
            libs.bundles.apache,
            libs.jetbrainsAnnotations,
            project(":orchestration-shared"),
            project(":client-registry-api"),
            project(":doc-checking-app-api")

    runtimeOnly platform(libs.openTelemetryBom),
            libs.openTelemetryAwsSdk

    implementation project(path: ':ipv-api')

    testImplementation libs.bundles.test,
            libs.awsLambdaTests,
            project(path: ':ipv-api'),
            project(":orchestration-shared-test"),
            libs.bundles.awsLambda,
            libs.awsSqs,
            libs.awsKms,
            libs.bundles.awsDynamoDb,
            libs.pactConsumer

    testRuntimeOnly libs.bundles.testRuntime
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/oidc/contract/**'
}

var pactDir = "$rootDir/oidc-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include '/uk/gov/di/authentication/oidc/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    if (isDevDeployBuild) {
        destinationDirectory = new File(terraformDir, "artifacts")
    }
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
