plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.17"
}

group = "uk.gov.di.authentication.oidc"
version = "unspecified"

configurations {
    compileOnly.extendsFrom(
            kms,
            lambda,
            sqs,
            sns,
            dynamodb
            )
    implementation.extendsFrom(
            govuk_notify,
            gson,
            nimbus,
            bouncycastle,
            cloudwatch,
            apache,
            jetbrains_annotations
            )
    testImplementation.extendsFrom(
            tests,
            lambda_tests,
            lambda,
            sqs,
            kms,
            dynamodb,
            pact_consumer
            )
    testRuntimeOnly.extendsFrom(test_runtime)
}

dependencies {
    implementation project(":orchestration-shared"),
            project(":client-registry-api"),
            project(":doc-checking-app-api")

    implementation project(path: ':ipv-api')

    testImplementation project(path: ':ipv-api'),
    project(":orchestration-shared-test")
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/oidc/contract/**'
}

var pactDir = "$rootDir/oidc-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include '/uk/gov/di/authentication/oidc/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
