plugins {
    id "java"
    id "jacoco"
    alias(libs.plugins.pact)
}

group = "uk.gov.di.authentication.oidc"
version = "unspecified"

dependencies {
    testImplementation(libs.lambda.tests)
    testImplementation(libs.bundles.aws.lambda)
    testImplementation(libs.aws.sqs)
    testImplementation(libs.aws.kms)
    testImplementation(libs.bundles.dynamodb)
    testImplementation(libs.pact.consumer)

    testRuntimeOnly(libs.junit.engine)

    compileOnly(libs.bundles.aws.lambda)
    compileOnly(libs.aws.kms)
    compileOnly(libs.aws.sqs)
    compileOnly(libs.aws.sns)
    compileOnly(libs.bundles.dynamodb)

    implementation(libs.govuk.notify)
    implementation(libs.jetbrains.annotations)

    implementation project(":orchestration-shared"),
            project(":client-registry-api"),
            project(":doc-checking-app-api")

    implementation(libs.aws.embedded.metrics)
    implementation(libs.bundles.apache)
    implementation(libs.bouncycastle)
    implementation(libs.bundles.nimbusds)
    implementation(libs.gson)

    implementation project(path: ':ipv-api')

    testImplementation project(path: ':ipv-api'),
    project(":orchestration-shared-test")

    testImplementation(libs.bundles.testing)
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/oidc/contract/**'
}

var pactDir = "$rootDir/oidc-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include '/uk/gov/di/authentication/oidc/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}
