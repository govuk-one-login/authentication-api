plugins {
    id "java"
    id "jacoco"
    id "au.com.dius.pact" version "4.6.17"
    id 'io.freefair.aspectj.post-compile-weaving' version '8.2.2'
}

group "uk.gov.di.authentication.oidc"
version "unspecified"

configurations.compileOnly {
    extendsFrom(configurations.kms)
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.sqs)
    extendsFrom(configurations.sns)
    extendsFrom(configurations.dynamodb)
}

configurations.implementation {
    extendsFrom(configurations.govuk_notify)
    extendsFrom(configurations.gson)
    extendsFrom(configurations.nimbus)
    extendsFrom(configurations.bouncycastle)
    extendsFrom(configurations.cloudwatch)
    extendsFrom(configurations.apache)
    extendsFrom(configurations.jetbrains_annotations)
}

configurations.runtimeOnly.extendsFrom(configurations.open_telemetry)

configurations.testImplementation {
    extendsFrom(configurations.tests)
    extendsFrom(configurations.lambda_tests)
    extendsFrom(configurations.lambda)
    extendsFrom(configurations.sqs)
    extendsFrom(configurations.kms)
    extendsFrom(configurations.dynamodb)
    extendsFrom(configurations.pact_consumer)
}

configurations.testRuntimeOnly.extendsFrom(configurations.test_runtime)

dependencies {
    implementation project(":orchestration-shared"),
            project(":client-registry-api"),
            project(":doc-checking-app-api"),
            "org.aspectj:aspectjrt:1.9.20.1"

    aspect "software.amazon.lambda:powertools-tracing:1.18.0"

    implementation project(path: ':ipv-api')

    testImplementation project(path: ':ipv-api'),
    project(":orchestration-shared-test")
}

test {
    useJUnitPlatform()
    environment "TRACING_ENABLED", "false"
    exclude 'uk/gov/di/authentication/oidc/contract/**'
}

var pactDir = "$rootDir/oidc-api/src/test/pact"

task pactConsumerTests (type: Test, group: "verification") {
    useJUnitPlatform()
    include '/uk/gov/di/authentication/oidc/contract/**'
    systemProperties['pact.rootDir'] = pactDir
}

pact {
    publish {
        pactDirectory = pactDir
        pactBrokerUrl = "$System.env.PACT_URL".toString() + "?testSource=" + "$System.env.PACT_BROKER_SOURCE_HEADER".toString()
        pactBrokerUsername = "$System.env.PACT_USER".toString()
        pactBrokerPassword = "$System.env.PACT_PASSWORD".toString()
        consumerVersion = "$System.env.CONSUMER_APP_VERSION".toString()
        consumerBranch = "${System.env.GIT_BRANCH}".toString()
        tags = [
            "${System.env.GIT_BRANCH}".toString()
        ]
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jacocoTestReport {
    reports {
        xml.setRequired true
    }
    dependsOn "test"
}
